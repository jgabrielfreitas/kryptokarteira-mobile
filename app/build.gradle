apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["compileSDK"]
    buildToolsVersion globalConfiguration["buildToolsVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["minSDK"]
        targetSdkVersion globalConfiguration["targetSDK"]
        applicationId "br.ufs.hiring.stone"
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    signingConfigs {
        exercise {
            storeFile file("../exercisekeystore")
            storePassword "123456"
            keyAlias 'exercise'
            keyPassword "123456"
        }
    }

    flavorDimensions "envs"

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            multiDexEnabled = true
            signingConfig signingConfigs.exercise
        }

        live {
            signingConfig signingConfigs.exercise
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'
    }
}

androidExtensions {
    experimental = true
}

dependencies {

    def libs = rootProject.ext

    implementation project(':data')
    implementation project(':core-architecture')
    implementation project(':features')

    implementation libs.rxJava
    implementation libs.rxAndroid
    implementation libs.gson
    implementation libs.hawk

    implementation libs.room
    implementation libs.roomRxJava2
    kapt libs.roomCompiler

    libs.kotlinRelated.each { implementation it }
    libs.kodeinRelated.each { implementation it }
    libs.retrofitRelated.each { implementation it }
    libs.archComponentsRelated.each { implementation it }
    libs.mobileUIRelated.each { implementation it }

    libs.unitTestingAndroidRelated.each { testImplementation it }
    libs.mockitoDexRelated.each { mockImplementation it }
    libs.instrumentationTestingRelated.each { androidTestImplementation it }
}


configurations.all {
    resolutionStrategy {
        def libs = rootProject.ext
        libs.toBeForced.each { force it}
    }
}